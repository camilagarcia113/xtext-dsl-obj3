/*
 * generated by Xtext
 */
package ar.edu.unq.validation

import ar.edu.unq.aventuraGrafica.AventuraGraficaPackage
import ar.edu.unq.aventuraGrafica.Juego
import ar.edu.unq.services.AventuraGraficaGrammarAccess
import com.google.inject.Inject
import java.util.List
import org.eclipse.xtext.validation.Check

import static ar.edu.unq.aventuraGrafica.AventuraGraficaPackage.Literals.*
import ar.edu.unq.aventuraGrafica.IrA
import ar.edu.unq.aventuraGrafica.Accion

/**
 * This class contains custom validation rules. 
 *
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class AventuraGraficaValidator extends AbstractAventuraGraficaValidator {

	@Check
	def checkNombreObjetoNoRepetido(Juego juego) {
		objetosRepetidos(juego).forEach [
			error("El nombre esta repetido",it,OBJETO__NAME)
		]
	}
	
	def objetosRepetidos(Juego juego) {
		juego.objetos.filter[juego.objetosConNombre(name) > 1]
	}
	
	def int objetosConNombre(Juego it, String nombre) {
		objetos.filter[name == nombre].size
	}
	
	@Check
	def checkNombreHabitacionNoRepetido(Juego juego) {
		val List<String> nombres = newArrayList()
		juego.habitaciones.forEach [ habitacion |
			if (nombres.contains(habitacion.name))
				error(
					"El nombre esta repetido",
					habitacion,
					habitacion.eClass.getEStructuralFeature(AventuraGraficaPackage.HABITACION__NAME
					)
				)
			else
				nombres.add(habitacion.name)
		]
	}
	
	@Check
	def checkHabitacionContieneUnObjetoConAccionDeSalida(Juego juego){
		juego.habitaciones.forEach[ h | 
			var objeto = h.objetos.findFirst[ o | 
				o.acciones.exists[ isIra ]
			]
			if(objeto == null && !h.isFinal ){
				error(
					"La habitación no contiene un objeto con acción de salida",
					h,
					h.eClass.getEStructuralFeature(AventuraGraficaPackage.HABITACION__NAME
					)
				)
			}
		]
	}
	
	def dispatch isIra(IrA it) { true }
	def dispatch isIra(Accion it) { false }
	
	@Check
	def checkHabitacionConObjetoACambiarIncluidoEnSuListaDeObjetos(Juego juego){
		juego.habitaciones.forEach[ h |
			if(!h.objetos.contains(h.objetoACambiar)){
				error(
					"La habitación no contiene el objeto a cambiar en su lista de objetos",
					h,
					h.eClass.getEStructuralFeature(AventuraGraficaPackage.HABITACION__NAME
					)
				)
			}
		]
	}
}
